buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def eclipseProperties = new Properties()
file('project.properties').withInputStream {
    eclipseProperties.load(it)
}

android {
    compileSdkVersion 28

    defaultConfig {
        targetSdkVersion eclipseProperties.getProperty('target') - 'android-'
        versionCode 'git rev-list --count HEAD'.execute(null, projectDir).text.toInteger()
        versionName 'git describe --tags --always HEAD'.execute(null, projectDir).text.trim()
        minSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    dependencies {
        implementation 'com.android.support:preference-v7:' + support_version
    }
}
repositories {
    mavenCentral()
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
